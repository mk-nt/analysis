print("Timestamp:", time("year"), time("day"), time("seconds"))
seed(0)

dummy <- readDiscreteCharacterData("project%PID%.01.nex")

nTaxa <- dummy.size()
nChar <- dummy.nchar()

# TODO reduce thinning
thin = 24
trees = readTreeTrace("%SCRIPTBASE%_run_%RUN%.trees", burnin = %BURNIN%, thinning = thin).getTrees()
params = readTrace("%SCRIPTBASE%_run_%RUN%.treep", burnin = %BURNIN%, thinning = thin)
treep = readTrace("%SCRIPTBASE%_run_%RUN%.trees", burnin = %BURNIN%, thinning = thin)
run = %RUN%
neo_betas = params[9].getValues()
mp[1] = abs(params[5].getValues())
mp[2] = abs(params[6].getValues())
mp[3] = abs(params[7].getValues())
mp[4] = abs(params[8].getValues())
rate_log_sd = params[10].getValues()
tree_lengths = params[11].getValues()

if (trees.size() != tree_lengths.size()) {
  write("Number of trees and parameters do not match",
        trees.size(), "!=", tree_lengths.size())
  stoq()
}

logFile = "%SCRIPTBASE%_run_%RUN%.pinv.log"

write("p0", "p1", "pInv\n", filename = logFile, append = FALSE)
for (i in 1:trees.size()) {
  if (neo_betas[i] < 0.05) {
    write("Error: beta ", neo_betas[i], "< 0.05 at i = ", i)
    next
  }
  # It would be more robust to replace this with 
  # fnDiscretizeDistribution( dnBeta (neo_betas[i], neo_betas[i]), 4)
  # See: https://github.com/revbayes/revbayes/issues/167
  # This came to light midway through analyses, so for consistent results,
  # we have retained:
  neo_beta_cats = fnDiscretizeBeta(neo_betas[i], neo_betas[i], 4)
  for (bc in 1:neo_beta_cats.size()) {
    neoQ[bc] = fnF81( Simplex(abs(1 - neo_beta_cats[bc]), neo_beta_cats[bc]) )
  }
  
  if (abs(tree_lengths[i] - trees[i].treeLength()) > 0.001) {
    write("Error: ", tree_lengths[i], "!=", trees[i].treeLength(), "at i = ", i)
  } else {
    m_morph ~ dnPhyloCTMC(
      tree = trees[i],
      siteRates = fnDiscretizeDistribution( dnLognormal( 0, rate_log_sd[i] ), 6),
      Q = neoQ,
      siteMatrices = Simplex(mp[1][i], mp[2][i], mp[3][i], mp[4][i]),
      type = "Standard",
      coding = "all"
    );
    
    dummy.excludeAll();
    dummy.includeCharacter(1);
    m_morph.setValue(dummy);
    p0 = m_morph.probability();
    
    dummy.excludeAll();
    dummy.includeCharacter(2);
    m_morph.setValue(dummy);
    p1 = m_morph.probability();
    
    p = p0 + p1;
    for (char in 3:nChar) {
      dummy.excludeAll();
      dummy.includeCharacter(char);
      m_morph.setValue(dummy);
      p += m_morph.probability();
    }
    screenFreq = 25
    if (floor(i / screenFreq) == i / screenFreq) {
      write(i, trees.size(), p0, p1, p)
    }
    write(p0, p1, p, "\n", filename = logFile, append = TRUE)
  }
}
q()
